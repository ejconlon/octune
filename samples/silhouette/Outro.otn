module Silhouette.Outro

self = [
    [+ r l bass drums +]
    coda
    introClosing
]

r = [
    |4> q_ eA5 q.F#5 'qE5
    |4> qE5 eA5 q.F#5 'qE5
    |4> qE5 eB5 q.F#5 qE5
    |4> 'hD5 qB4 qC#5
    |4> 'hD5 'hD5
    |4> qF#5 qE5 qD5 qB4
]

l = [
    |4> 4B3
    |4> '4C#4
    |4> 4C#4
    |4> 'hD4 qD4 qC#4
    |4> 4B3
    |4> 4C#4
]

bass = [
    |4> 'q.G2 'q.G2 'qG2
    |4> 'q.A2 'q.A2 'qA2
    |4> 'q.A#2 'q.A#2 'qA#2
    |4> [ [+ 'hB2 'hB3 +] [+ qB2 qB3 +] [+ qA2 qA3 +] ]
    |4> 'q.G2 'q.G2 'qG2
    |4> 'q.A2 'q.A2 'qA2
]

drums = [
    |16> [* 3 : e%% e% e% e%% e% e% e%% e% *]
         h%% q% q%
    |8> [* 4 : Silhouette.Common.drumRiff *]
]

coda = [+ codaR woOh codaL [^ SAWTOOTH : codaGuitarLine ^] codaBass codaDrums +]

codaR = [
    |4> eE5 eD5 eE5 eD5 hD5
    |4> 4D5
]

codaL = [* 2 :
    |4> '4A3
    |4> '4D4
    |4> hD4 hB3
    |4> hA#3 hC#4
    |4> '4D4
    |4> '4C#4
    |4> '4B3
    |4> '4C#4
*]

woOh = [
    |48>
    [* 3 :
        |4> h_ 'qF#4 qE4
        |4> h_ 'qE4 qD4
        |4> h_ 'qF#4 qE4
        |4> h_ 'qE4 qF#4
    *]
    |16>
    [*2 :
        |4> h_ 'qF#4 qE4
        |4> h_ 'qE4 qD4
    *]
]

codaGuitarLine = [
    |4> eD4 eC#4 eD4 q.A4 eG4 eF#4
    |4> eG4 eF#4 eD4 q.A4 eG4 eF#4
    |4> eG4 eF#4 eD4 q.D5 sC#5 sD5 sC#5 sA4
    |4> q.F#4 q.G4 qA4
    |4> q.D5 q.A5 eG5 eF#5
    |4> '4D5
    |4> qA3 qB3 qD4 qG4
    |4> qF#4 qG4 qA4 qD5

    |4> q.G5 q.F#5 qD5
    |4> '4D5
    |4> q.G5 q.F#5 qG5
    |4> q.A5 q.B5 qC#6
    |4> q.D6 q.C#6 qB5
    |4> 4A5
    |4> q.G5 q.F#5 qG5
    |4> q.A5 q.B5 qC#6

    |8> 8D6
]

codaBass = [
    [* 2 :
        |4> 'q.F#2 'q.F#2 'qF#2
        |4> 'q.G2 'q.G2 'qG2
        |4> 'q.A2 'q.A2 'qA2
        |4> 'q.A#2 'q.A#2 'qA#2
        |4> 'q.B2 'q.B2 'qB2
        |4> 'q.F#2 'q.F#2 'qF#2
        |4> 'q.G2 'q.G2 'qG2
        |4> 'q.A2 'q.A2 'qA2
    *]

    [* 2 :
        |4> [+ hB2 [ e_ [* 3 : [+ 'eF#3 'eB3 +] *] ] +]
            [+ hA2 [ e_ [* 3 : [+ 'eF#3 'eA3 +] *] ] +]
        |4> [+ hG2 [ e_ [* 3 : [+ 'eD3 'eG3 +] *] ] +]
            [+ hA2 [ e_ [* 3 : [+ 'eE3 'eA3 +] *] ] +]
    *]
]

codaDrums = [
    [* 64 : e%% e% *]

    |16> [* 14 : q%% *]
         e%% s% s% q%%
]

introClosing = [+ introClosingR introClosingBass introClosingDrums +]

introClosingR = [
    |8> Silhouette.Intro.r1Riff eG5 eF#5 eD5
    |8> Silhouette.Intro.r1Riff 'eD5 eD5 eE5
]

introClosingBass = [
    |4> [+ hB2 [ e_ [* 3 : [+ 'eF#3 'eB3 +] *] ] +]
        [+ hA2 [ e_ [* 3 : [+ 'eF#3 'eA3 +] *] ] +]
    |4> [+ hG2 [ e_ [* 3 : [+ 'eD3 'eG3 +] *] ] +]
        [+ hA2 [ e_ [* 3 : [+ 'eE3 'eA3 +] *] ] +]

    |4> [+ hB2 [ 'eB3 [* 3 : [+ 'eF#3 'eB3 +] *] ] +]
        [+ hA2 [ 'eA3 [* 3 : [+ 'eF#3 'eA3 +] *] ] +]
    |4> [+ hG2 [ 'eG3 [* 3 : [+ 'eD3 'eG3 +] *] ] +]
        [+ hA2 [ 'eA3 [* 3 : [+ 'eE3 'eA3 +] *] ] +]
]

introClosingDrums = [
    [* 8 : e%% e% *]
    |8> e_ q%% q.%% e% q%% q%% e% [* 4 : e%% *]
]
